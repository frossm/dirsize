rem ----------------------------------------------------------
rem Take Command script to generate MD5 & SHA Checksums
rem
rem Run this as "tools\GenHashes.btm" from the project root
rem ----------------------------------------------------------
setlocal

echo %_cwd
set ChecksumFile=target\CHECKSUM
set JarFilename=target\%@Field["\",-1,%@PATH[%@Full[pom.xml]]].jar

:: Ensure the jarfile exists
iff not exist %JarFilename then
	echo %JarFilename does not exist
	echo Run this from the root of the project directory
	quit /b
endiff

:: Delete an old checksum file if it exists
iff exist %ChecksumFile.md5 then
	del %ChecksumFile%* /Q
endiff

:: Generate Hashes
echo %@Lower[%@MD5[%JarFilename]]  %@Filename[%JarFilename] > %ChecksumFile%.md5
echo %@Lower[%@SHA1[%JarFilename]]  %@Filename[%JarFilename] > %ChecksumFile%.SHA1
echo %@Lower[%@SHA256[%JarFilename]]  %@Filename[%JarFilename] > %ChecksumFile%.SHA256

:: Display the file to the console
echos %@Filename[%ChecksumFile].MD5:^t
type %ChecksumFile%.md5

echos %@Filename[%ChecksumFile].SHA1:^t
type %ChecksumFile%.SHA1

echos %@Filename[%ChecksumFile].SHA256:
type %ChecksumFile%.SHA256

:: Cleanup and exit
unset ChecksumFile,JarFilename
endlocal
